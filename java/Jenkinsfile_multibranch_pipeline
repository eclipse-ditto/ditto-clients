#!groovy
pipeline {
    agent none
    environment {
        // Need to replace the '%2F' used by Jenkins to deal with / in the path (e.g. story/...)
        theBranch = "${env.BRANCH_NAME}".replace("%2F", "-").replace("/", "-")
        theVersion = "0-${theBranch}-SNAPSHOT"
        dittoVersion = "${theVersion}"
        theMvnRepo = "$WORKSPACE/../feature-repository-${theBranch}";
        JAVA_TOOL_OPTIONS = '-Duser.home=/home/jenkins-slave'
    }
    stages {
        stage('Choose Ditto Version to build against') {
            steps {
                script {
                    dittoVersion = askForDittoVersionToUse(dittoVersion)
                    echo "Selected Ditto version: ${dittoVersion}"
                }
            }
        }
        stage('Checkout scm') {
            agent {
                label 'docker-slave'
            }
            steps {
                echo 'Checkout scm'
                checkout scm
            }
        }
        stage('Build') {
            agent {
                docker {
                    args '-v /home/jenkins-slave/.m2:/home/jenkins-slave/.m2:z'
                    image 'thingsdev.azurecr.io/things-pipeline/maven-jdk-8:latest'
                }
            }
            steps {
                dir("$WORKSPACE/java") {
                    configFileProvider([configFile(fileId: 'mvn-bdc-settings', variable: 'MVN_SETTINGS')]) {
                        sh "mvn -s $MVN_SETTINGS clean deploy javadoc:jar source:jar" +
                                " --batch-mode --errors" +
                                " -DcreateJavadoc=true" +
                                " -Drevision=${theVersion}" +
                                " -Dditto.version=${dittoVersion}" +
                                " -Pditto-java-client"
                    }
                }
            }
        }
        stage('SonarQube Scan') {
            agent {
                docker {
                    args '-v /home/jenkins-slave/.m2:/home/jenkins-slave/.m2:z'
                    image 'thingsdev.azurecr.io/things-pipeline/maven-jdk-8:latest'
                }
            }
            steps {
                dir("$WORKSPACE/java") {
                    configFileProvider([configFile(fileId: 'mvn-bdc-settings', variable: 'MVN_SETTINGS')]) {
                        withSonarQubeEnv("${env.SONAR_QUBE_ENV}") {
                            sh "mvn -s $MVN_SETTINGS --batch-mode --errors sonar:sonar -Dsonar.branch.name=${theBranch} " +
                                    " -Drevision=${theVersion}" +
                                    " -Dditto.version=${dittoVersion}"
                        }
                    }
                }
            }
        }
    }
}

def askForDittoVersionToUse(defaultVersion) {
    try {
        timeout(time: 1, unit: 'MINUTES') {
            def branch = input message: 'Which Ditto version to use?',
                    parameters: [string(defaultValue: defaultVersion, description: 'Branch', name: 'DITTO-BRANCH')]
            return branch
        }
    } catch (e) {
        return defaultVersion
    }
}
